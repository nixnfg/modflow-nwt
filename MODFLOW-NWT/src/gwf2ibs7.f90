   !   December, 2005 -- updated to work with MODFLOW-2005
   !   September, 2000 -- updated to work with MODFLOW-2000
   !   May, 2000 -- fixed error that caused incorrect critical head values
   !   to be written in an external file when the option to write an
   !   external file (IHCSV>0) is used.
   !   June, 1996 -- 3 statements in the version documented
   !   in TWRI 6-A2 have been modified in order to correct a problem.
   !   Although subsidence is only meant to be active for layers in which
   !   IBQ>0, some of the subroutines performed subsidence calculations when
   !   IBQ<0.  Note that this was a problem only if negative IBQ values
   !   were specified.  That is, the code has always worked correctly for
   !   IBQ=0 and IBQ>0.
   !   September, 2003 -- added the following:
   !    1. Print a warning message that the IBS1 Package has been supseseded
   !       by the SUB Package.
   !    2. If the SUB Package and the IBS package are used simultaneously,
   !       stop the simulation.
   !
MODULE GWFIBSMODULE
   INTEGER, SAVE, POINTER    ::IIBSCB,IIBSOC,ISUBFM,ICOMFM,IHCFM
   INTEGER, SAVE, POINTER    ::ISUBUN,ICOMUN,IHCUN
   INTEGER, SAVE,    DIMENSION(:),     POINTER ::IBQ
   INTEGER, SAVE,    DIMENSION(:),     POINTER ::IBQ1
   REAL,    SAVE,    DIMENSION(:,:,:), POINTER ::HC
   REAL,    SAVE,    DIMENSION(:,:,:), POINTER ::SCE
   REAL,    SAVE,    DIMENSION(:,:,:), POINTER ::SCV
   REAL,    SAVE,    DIMENSION(:,:,:), POINTER ::SUB
   TYPE GWFIBSTYPE
      INTEGER,POINTER    ::IIBSCB,IIBSOC,ISUBFM,ICOMFM,IHCFM
      INTEGER,POINTER    ::ISUBUN,ICOMUN,IHCUN
      INTEGER,    DIMENSION(:),     POINTER ::IBQ
      INTEGER,    DIMENSION(:),     POINTER ::IBQ1
      REAL,       DIMENSION(:,:,:), POINTER ::HC
      REAL,       DIMENSION(:,:,:), POINTER ::SCE
      REAL,       DIMENSION(:,:,:), POINTER ::SCV
      REAL,       DIMENSION(:,:,:), POINTER ::SUB
   END TYPE GWFIBSTYPE
   TYPE(GWFIBSTYPE),SAVE ::GWFIBSDAT(10)
END MODULE GWFIBSMODULE



SUBROUTINE GWF2IBS7AR(IN,INSUB,IGRID)
   !
   !  Based on
   !-----VERSION 07JUN1996 GWF1IBS6ALP AND VERSION 1117 02JUN1988 GWF1IBS6RPP
   !-----VERSION 01AUG1996 -- modified to allow 200 layers instead of 80
   !     ******************************************************************
   !     ALLOCATE ARRAY STORAGE FOR INTERBED STORAGE PACKAGE
   !     READ INTERBED STORAGE DATA
   !     ******************************************************************
   !
   !        SPECIFICATIONS:
   !     ------------------------------------------------------------------
   USE GLOBAL,       ONLY: NCOL,NROW,NLAY,IOUT,DELR,DELC,HNEW
   USE GWFIBSMODULE, ONLY: HC,SCE,SCV,SUB,IBQ,IBQ1,IIBSCB,IIBSOC,&
   &ISUBFM,ICOMFM,IHCFM,ISUBUN,ICOMUN,IHCUN
   CHARACTER*24 ANAME(4)
   !
   DATA ANAME(1) /'   PRECONSOLIDATION HEAD'/
   DATA ANAME(2) /'ELASTIC INTERBED STORAGE'/
   DATA ANAME(3) /' VIRGIN INTERBED STORAGE'/
   DATA ANAME(4) /'     STARTING COMPACTION'/
   !     ------------------------------------------------------------------
   !
   ALLOCATE(IIBSCB,IIBSOC,ISUBFM,ICOMFM,IHCFM,ISUBUN,ICOMUN,IHCUN)
   ALLOCATE(IBQ(NLAY),IBQ1(NLAY))
   !
   !1------IDENTIFY PACKAGE.
   WRITE(IOUT,1)IN
1  FORMAT(1H0,'IBS -- INTERBED STORAGE PACKAGE, VERSION 7,',&
   &' 12/27/2005',' INPUT READ FROM UNIT',I3)
   !1a------PRINT WARNING MESSAGE THAT PACKAGE HAS BEEN SUPERSEDED.
   WRITE(IOUT,103)
103 FORMAT(1H0,'***NOTICE*** AS OF SEPTEMBER 2003, THE INTERBED ',&
   &'STORAGE PACKAGE HAS ',/,&
   &'BEEN SUPERSEDED BY THE SUBSIDENCE AND AQUIFER-SYSTEM ',&
   &'COMPACTION PACKAGE.',/,' SUPPORT FOR IBS MAY BE ',&
   &'DISCONTINUED IN THE FUTURE.')
   !1b------PRINT A MESSAGE AND STOP THE SIMULATION OF BOTH IBS AND SUB
   !1b------ ARE USED.
   IF(INSUB.GT.0) THEN
      WRITE(IOUT,104)
104   FORMAT(1H0,'***ERROR*** THE IBS AND SUB PACKAGE SHOULD ',&
      &'NOT BOTH BE USED IN ',/,&
      &'THE SAME SIMULATION. ********STOPPING******** ')
      CALL USTOP(' ')
   ENDIF
   !
   !4------READ FLAG FOR STORING CELL-BY-CELL STORAGE CHANGES AND
   !4------FLAG FOR PRINTING AND STORING COMPACTION, SUBSIDENCE, AND
   !4------CRITICAL HEAD ARRAYS.
   READ(IN,3) IIBSCB,IIBSOC
3  FORMAT(2I10)
   !
   !5------IF CELL-BY-CELL TERMS TO BE SAVED THEN PRINT UNIT NUMBER.
   IF(IIBSCB.GT.0) WRITE(IOUT,105) IIBSCB
105 FORMAT(1X,'CELL-BY-CELL FLOW TERMS WILL BE SAVED ON UNIT',I3)
   !
   !5A-----IF OUTPUT CONTROL FOR PRINTING ARRAYS IS SELECTED PRINT MESSAGE.
   IF(IIBSOC.GT.0) WRITE(IOUT,106)
106 FORMAT(1X,'OUTPUT CONTROL RECORDS FOR IBS PACKAGE WILL BE ',&
   &'READ EACH TIME STEP.')
   !
   !6------READ INDICATOR AND FIND OUT HOW MANY LAYERS HAVE INTERBED STORAGE.
   READ(IN,110) (IBQ(K),K=1,NLAY)
110 FORMAT(40I2)
   NAQL=0
   DO 120 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 120
      NAQL=NAQL+1
      IBQ1(NAQL)=K
120 CONTINUE
   !
   !7------IDENTIFY WHICH LAYERS HAVE INTERBED STORAGE.
   WRITE(IOUT,130) (IBQ1(K),K=1,NAQL)
130 FORMAT(1X,'INTERBED STORAGE IN LAYER(S) ',80I2)
   !
   !8------ALLOCATE SPACE FOR THE ARRAYS HC, SCE, SCV, AND SUB.
   IF(NAQL.GT.0) THEN
      ALLOCATE(HC(NCOL,NROW,NAQL))
      ALLOCATE(SCE(NCOL,NROW,NAQL))
      ALLOCATE(SCV(NCOL,NROW,NAQL))
      ALLOCATE(SUB(NCOL,NROW,NAQL))
   ELSE
      ALLOCATE(HC(1,1,1))
      ALLOCATE(SCE(1,1,1))
      ALLOCATE(SCV(1,1,1))
      ALLOCATE(SUB(1,1,1))
   END IF
   !
   !9------READ IN STORAGE AND CRITICAL HEAD ARRAYS
   KQ=0
   DO 160 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 160
      KQ=KQ+1
      CALL U2DREL(HC(:,:,KQ),ANAME(1),NROW,NCOL,K,IN,IOUT)
      CALL U2DREL(SCE(:,:,KQ),ANAME(2),NROW,NCOL,K,IN,IOUT)
      CALL U2DREL(SCV(:,:,KQ),ANAME(3),NROW,NCOL,K,IN,IOUT)
      CALL U2DREL(SUB(:,:,KQ),ANAME(4),NROW,NCOL,K,IN,IOUT)
160 CONTINUE
   !
   !10-----LOOP THROUGH ALL CELLS WITH INTERBED STORAGE.
   KQ=0
   DO 180 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 180
      KQ=KQ+1
      DO 170 IR=1,NROW
         DO 170 IC=1,NCOL
   !
   !11-----MULTIPLY STORAGE BY AREA TO GET STORAGE CAPACITY.
            AREA=DELR(IC)*DELC(IR)
            SCE(IC,IR,KQ)=SCE(IC,IR,KQ)*AREA
            SCV(IC,IR,KQ)=SCV(IC,IR,KQ)*AREA
   !
   !12-----MAKE SURE THAT PRECONSOLIDATION HEAD VALUES
   !12-----ARE CONSISTANT WITH STARTING HEADS.
            IF(HC(IC,IR,KQ).GT.HNEW(IC,IR,K)) HC(IC,IR,KQ)=HNEW(IC,IR,K)
170   CONTINUE
180 CONTINUE
   !
   !13-----INITIALIZE AND READ OUTPUT FLAGS.
   ICOMFM=0
   ISUBFM=0
   IHCFM=0
   ICOMUN=0
   ISUBUN=0
   IHCUN=0
   IF(IIBSOC.LE.0) GO TO 200
   READ(IN,190) ISUBFM,ICOMFM,IHCFM,ISUBUN,ICOMUN,IHCUN
190 FORMAT(6I10)
   WRITE(IOUT,191) ISUBFM,ICOMFM,IHCFM
191 FORMAT(1H0,'    SUBSIDENCE PRINT FORMAT IS NUMBER',I4/&
   &'     COMPACTION PRINT FORMAT IS NUMBER',I4/&
   &'  CRITICAL HEAD PRINT FORMAT IS NUMBER',I4)
   IF(ISUBUN.GT.0) WRITE(IOUT,192) ISUBUN
192 FORMAT(1H0,'    UNIT FOR SAVING SUBSIDENCE IS',I4)
   IF(ICOMUN.GT.0) WRITE(IOUT,193) ICOMUN
193 FORMAT(1H ,'    UNIT FOR SAVING COMPACTION IS',I4)
   IF(IHCUN.GT.0)  WRITE(IOUT,194) IHCUN
194 FORMAT(1H ,' UNIT FOR SAVING CRITICAL HEAD IS',I4)
   !
   !14-----RETURN
200 CALL SGWF2IBS7PSV(IGRID)
   RETURN
END
SUBROUTINE GWF2IBS7ST(KPER,IGRID)
   !
   !-----Based on VERSION 15SEPT2000 GWF1IBS6ST
   !     ******************************************************************
   !     CHECK THAT NO STREE PERIOD IS STEADY STATE EXCEPT THE FIRST, AND
   !     SET HC EQUAL TO THE STEADY-STATE HEAD IF STEADY-STATE HEAD IS
   !     LOWER THAN HC.
   !     ******************************************************************
   !
   !        SPECIFICATIONS:
   !     ------------------------------------------------------------------
   USE GLOBAL,       ONLY: NCOL,NROW,NLAY,ISSFLG,HNEW,IOUT
   USE GWFIBSMODULE, ONLY:HC,IBQ
   !     ------------------------------------------------------------------
   !
   !-------SET POINTERS FOR THE CURRENT GRID.
   CALL SGWF2IBS7PNT(IGRID)
   !
   !1------STOP if steady state after 1st stress period.
   IF(KPER.GT.1 .AND. ISSFLG(KPER).NE.0) THEN
      WRITE(IOUT,8)
8     FORMAT(1X,'INTERBED STORAGE INAPPROPRIATE FOR A STEADY-STATE',&
      &/,1X,'STRESS PERIOD AFTER PERIOD 1 -- SIMULATION STOPPING.')
      CALL USTOP(' ')
   END IF
   !
   !4------MAKE SURE THAT PRECONSOLIDATION HEAD VALUES
   !4------ARE CONSISTANT WITH STEADY-STATE HEADS.
   IF(KPER.EQ.2 .AND. ISSFLG(1).NE.0) THEN
      KQ=0
      DO 80 K=1,NLAY
         IF(IBQ(K).GT.0) THEN
            KQ=KQ+1
            DO 70 IR=1,NROW
               DO 70 IC=1,NCOL
                  HTMP=HNEW(IC,IR,K)
                  IF(HC(IC,IR,KQ).GT.HTMP) HC(IC,IR,KQ)=HTMP
70          CONTINUE
         END IF
80    CONTINUE
   END IF
   !
   RETURN
END
SUBROUTINE GWF2IBS7FM(KPER,IGRID)
   !
   !  Based on:
   !-----VERSION 07JUN1996 GWF1IBS6FM
   !-----VERSION 01AUG1996 -- modified to allow 200 layers instead of 80
   !     ******************************************************************
   !        ADD INTERBED STORAGE TO RHS AND HCOF
   !     ******************************************************************
   !
   !        SPECIFICATIONS:
   !     ------------------------------------------------------------------
   USE GLOBAL,       ONLY:NCOL,NROW,NLAY,RHS,HCOF,HNEW,HOLD,IBOUND,&
   &ISSFLG
   USE GWFBASMODULE, ONLY:DELT
   USE GWFIBSMODULE, ONLY:HC,SCE,SCV,IBQ
   !     ------------------------------------------------------------------
   !
   !-------SET POINTERS FOR THE CURRENT GRID.
   CALL SGWF2IBS7PNT(IGRID)
   !
   !0------Return if stress period is steady state.
   ISSF=ISSFLG(KPER)
   IF(ISSF.NE.0) RETURN
   !
   !1------INITIALIZE
   TLED=1./DELT
   KQ=0
   !
   !2------FIND LAYERS WITH INTERBED STORAGE
   DO 110 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 110
      KQ=KQ+1
      DO 100 I=1,NROW
         DO 100 J=1,NCOL
            IF(IBOUND(J,I,K).LE.0) GO TO 100
   !
   !3------DETERMINE STORAGE CAPACITIES FOR CELL AT START AND END OF STEP
            RHO1=SCE(J,I,KQ)*TLED
            RHO2=RHO1
            HCTMP=HC(J,I,KQ)
            IF(HNEW(J,I,K).LT.HCTMP) RHO2=SCV(J,I,KQ)*TLED
   !
   !4------ADD APPROPRIATE TERMS TO RHS AND HCOF
            RHS(J,I,K)=RHS(J,I,K)-HCTMP*(RHO2-RHO1)-RHO1*HOLD(J,I,K)
            HCOF(J,I,K)=HCOF(J,I,K)-RHO2
100   CONTINUE
110 CONTINUE
   !
   !5------RETURN
   RETURN
END
SUBROUTINE GWF2IBS7BD(KSTP,KPER,IGRID)
   !  Based on:
   !-----VERSION 07JUN1996 GWF1IBS6BD
   !-----VERSION 01AUG1996 -- modified to allow 200 layers instead of 80
   !     ******************************************************************
   !     CALCULATE VOLUMETRIC BUDGET FOR INTERBED STORAGE
   !     ******************************************************************
   !
   !     SPECIFICATIONS:
   !     ------------------------------------------------------------------
   USE GLOBAL,       ONLY:NCOL,NROW,NLAY,IBOUND,HOLD,HNEW,BUFF,&
   &DELR,DELC,ISSFLG,IOUT
   USE GWFBASMODULE, ONLY:DELT,VBVL,VBNM,MSUM,ICBCFL
   USE GWFIBSMODULE, ONLY:HC,SCE,SCV,SUB,IBQ,IIBSCB
   !
   CHARACTER*16 TEXT
   DATA TEXT /'INTERBED STORAGE'/
   !     ------------------------------------------------------------------
   !
   !-------SET POINTERS FOR THE CURRENT GRID.
   CALL SGWF2IBS7PNT(IGRID)
   ISSF=ISSFLG(KPER)
   !
   !1------INITIALIZE CELL-BY-CELL FLOW TERM FLAG (IBD) AND
   !1------ACCUMULATORS (STOIN AND STOUT).
   IBD=0
   STOIN=0.
   STOUT=0.
   !
   !2------TEST TO SEE IF CELL-BY-CELL FLOW TERMS ARE NEEDED.
   IF(ICBCFL.NE.0  .AND. IIBSCB.GT.0 ) THEN
   !
   !3------CELL-BY-CELL FLOW TERMS ARE NEEDED SET IBD AND CLEAR BUFFER.
      IBD=1
      DO 5 IL=1,NLAY
         DO 5 IR=1,NROW
            DO 5 IC=1,NCOL
               BUFF(IC,IR,IL)=0.
5     CONTINUE
   END IF
   !
   !4------RUN THROUGH EVERY CELL IN THE GRID WITH INTERBED STORAGE.
   KQ=0
   TLED=1.
   IF(ISSF.EQ.0) TLED=1./DELT
   DO 110 K=1,NLAY
      IF(IBQ(K).LE.0 .OR. ISSF.NE.0) GO TO 110
      KQ=KQ+1
      DO 100 I=1,NROW
         DO 100 J=1,NCOL
   !
   !5------CALCULATE FLOW FROM STORAGE (VARIABLE HEAD CELLS ONLY)
            IF(IBOUND(J,I,K).LE.0) GO TO 100
            HHOLD=HOLD(J,I,K)
            HHNEW=HNEW(J,I,K)
            HHC=HC(J,I,KQ)
   !
   !6------GET STORAGE CAPACITIES AT BEGINNING AND END OF TIME STEP.
            SBGN=SCE(J,I,KQ)
            SEND=SBGN
            IF(HHNEW.LT.HHC) SEND=SCV(J,I,KQ)
   !
   !7------CALCULATE VOLUME CHANGE IN INTERBED STORAGE FOR TIME STEP.
            STRG=HHC*(SEND-SBGN)+SBGN*HHOLD-SEND*HHNEW
   !
   !8------ACCUMULATE SUBSIDENCE ASSOCIATED WITH CHANGE IN STORAGE
            SUB(J,I,KQ)=SUB(J,I,KQ)+STRG/(DELR(J)*DELC(I))
   !
   !9------IF C-B-C FLOW TERMS ARE TO BE SAVED THEN ADD RATE TO BUFFER.
            IF(IBD.EQ.1) BUFF(J,I,K)=BUFF(J,I,K)+STRG*TLED
   !
   !10-----SEE IF FLOW IS INTO OR OUT OF STORAGE.
            IF(STRG)94,100,96
94          STOUT=STOUT-STRG
            GO TO 100
96          STOIN=STOIN+STRG
100   CONTINUE
110 CONTINUE
   !
   !11-----IF C-B-C FLOW TERMS WILL BE SAVED CALL UBUDSV TO RECORD THEM.
   IF(IBD.EQ.1) CALL UBUDSV(KSTP,KPER,TEXT,IIBSCB,BUFF,NCOL,NROW,&
   &NLAY,IOUT)
   !
   !12-----MOVE RATES,VOLUMES & LABELS INTO ARRAYS FOR PRINTING.
200 VBVL(3,MSUM)=STOIN*TLED
   VBVL(4,MSUM)=STOUT*TLED
   VBVL(1,MSUM)=VBVL(1,MSUM)+STOIN
   VBVL(2,MSUM)=VBVL(2,MSUM)+STOUT
   VBNM(MSUM)=TEXT
   !
   !13-----INCREMENT BUDGET TERM COUNTER
   MSUM=MSUM+1
   IF(ISSF.NE.0) RETURN
   !
   !14-----UPDATE PRECONSOLIDATION HEAD ARRAY
   KQ=0
   DO 310 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 310
      KQ=KQ+1
      DO 300 I=1,NROW
         DO 300 J=1,NCOL
            IF(IBOUND(J,I,K).LE.0) GO TO 300
            HHNEW=HNEW(J,I,K)
            IF(HHNEW.LT.HC(J,I,KQ)) HC(J,I,KQ)=HHNEW
300   CONTINUE
310 CONTINUE
   !
   !15-----RETURN
   RETURN
END
SUBROUTINE GWF2IBS7OT(KSTP,KPER,IN,IGRID)
   !  Based on
   !-----VERSION 07JUN1996 GWF1IBS6OT
   !-----VERSION 01AUG1996 -- modified to allow 200 layers instead of 80
   !     ******************************************************************
   !     PRINT AND STORE SUBSIDENCE, COMPACTION AND CRITICAL HEAD.
   !     ******************************************************************
   !
   !     SPECIFICATIONS:
   !     ------------------------------------------------------------------
   USE GLOBAL,       ONLY:NCOL,NROW,NLAY,BUFF,NSTP,ISSFLG,IOUT
   USE GWFBASMODULE, ONLY:PERTIM,TOTIM
   USE GWFIBSMODULE, ONLY:IBQ,SUB,HC,IIBSOC,ISUBFM,ICOMFM,IHCFM,&
   &ISUBUN,ICOMUN,IHCUN
   CHARACTER*16 TEXT(3)
   DATA TEXT(1) /'      SUBSIDENCE'/
   DATA TEXT(2) /'      COMPACTION'/
   DATA TEXT(3) /'   CRITICAL HEAD'/
   !     ------------------------------------------------------------------
   !
   !-------SET POINTERS FOR THE CURRENT GRID.
   CALL SGWF2IBS7PNT(IGRID)
   ISSF=ISSFLG(KPER)
   IF(ISSF.NE.0) RETURN
   !
   !1------INITIALIZE FLAGS FOR PRINTING AND SAVING SUBSIDENCE, COMPACTION,
   !1------AND CRITICAL HEAD
   ISUBPR=0
   ICOMPR=0
   IHCPR=0
   ISUBSV=0
   ICOMSV=0
   IHCSV=0
   IF(KSTP.EQ.NSTP(KPER)) ISUBPR=1
   !2------READ FLAGS FOR PRINTING AND SAVING.
   IF(IIBSOC.LE.0) GO TO 28
   READ(IN,10) ISUBPR,ICOMPR,IHCPR,ISUBSV,ICOMSV,IHCSV
10 FORMAT(6I10)
   WRITE(IOUT,15) ISUBPR,ICOMPR,IHCPR,ISUBSV,ICOMSV,IHCSV
15 FORMAT(1H0,'FLAGS FOR PRINTING AND STORING SUBSIDENCE, ',&
   &'COMPACTION, AND CRITICAL HEAD:'/&
   &'   ISUBPR    ICOMPR    IHCPR     ISUBSV    ICOMSV    IHCSV   '/&
   &' ------------------------------------------------------------'/&
   &I6,5I10)
   !
   !3------PRINT AND STORE SUBSIDENCE, FIRST, CLEAR OUT BUFF.
28 IF(ISUBPR.LE.0.AND.ISUBSV.LE.0) GO TO 100
   DO 30 IR=1,NROW
      DO 30 IC=1,NCOL
         BUFF(IC,IR,1)=0.
30 CONTINUE
   !
   !4------SUM COMPACTION IN ALL LAYERS TO GET SUBSIDENCE.
   KQ=0
   DO 50 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 50
      KQ=KQ+1
      DO 40 I=1,NROW
         DO 40 J=1,NCOL
            BUFF(J,I,1)=BUFF(J,I,1)+SUB(J,I,KQ)
40    CONTINUE
50 CONTINUE
   !
   !5------PRINT SUBSIDENCE.
   IF(ISUBPR.LE.0) GO TO 60
   IF(ISUBFM.LT.0) CALL ULAPRS(BUFF,TEXT(1),KSTP,KPER,NCOL,NROW,1,&
   &-ISUBFM,IOUT)
   IF(ISUBFM.GE.0) CALL ULAPRW(BUFF,TEXT(1),KSTP,KPER,NCOL,NROW,1,&
   &ISUBFM,IOUT)
   !
   !6------STORE SUBSIDENCE.
60 IF(ISUBSV.LE.0) GO TO 100
   CALL ULASAV(BUFF,TEXT(1),KSTP,KPER,PERTIM,TOTIM,NCOL,NROW,1,&
   &ISUBUN)
   !
   !7------PRINT COMPACTION FOR ALL LAYERS WITH INTERBED STORAGE.
100 IF(ICOMPR.LE.0) GO TO 140
   KQ=0
   DO 130 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 130
      KQ=KQ+1
      IF(ICOMFM.LT.0) CALL ULAPRS(SUB(:,:,KQ),TEXT(2),KSTP,KPER,NCOL,&
      &NROW,K,-ICOMFM,IOUT)
      IF(ICOMFM.GE.0) CALL ULAPRW(SUB(:,:,KQ),TEXT(2),KSTP,KPER,NCOL,&
      &NROW,K,ICOMFM,IOUT)
130 CONTINUE
   !
   !8------SAVE COMPACTION FOR ALL LAYERS WITH INTERBED STORAGE.
140 IF(ICOMSV.LE.0) GO TO 200
   KQ=0
   DO 160 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 160
      KQ=KQ+1
      CALL ULASAV(SUB(:,:,KQ),TEXT(2),KSTP,KPER,PERTIM,TOTIM,NCOL,&
      &NROW,K,ICOMUN)
160 CONTINUE
   !
   !9------PRINT CRITICAL HEAD FOR ALL LAYERS WITH INTERBED STORAGE.
200 IF(IHCPR.LE.0) GO TO 240
   KQ=0
   DO 230 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 230
      KQ=KQ+1
      IF(IHCFM.LT.0) CALL ULAPRS(HC(:,:,KQ),TEXT(3),KSTP,KPER,NCOL,&
      &NROW,K,-IHCFM,IOUT)
      IF(IHCFM.GE.0) CALL ULAPRW(HC(:,:,KQ),TEXT(3),KSTP,KPER,NCOL,&
      &NROW,K,IHCFM,IOUT)
230 CONTINUE
   !
   !10-----SAVE CRITICAL HEAD FOR ALL LAYERS WITH INTERBED STORAGE.
240 IF(IHCSV.LE.0) GO TO 300
   KQ=0
   DO 260 K=1,NLAY
      IF(IBQ(K).LE.0) GO TO 260
      KQ=KQ+1
      CALL ULASAV(HC(:,:,KQ),TEXT(3),KSTP,KPER,PERTIM,TOTIM,NCOL,&
      &NROW,K,IHCUN)
260 CONTINUE
   !
   !11-----RETURN
300 RETURN
END
SUBROUTINE GWF2IBS7DA(IGRID)
   !  Deallocate IBS DATA
   USE GWFIBSMODULE
   !
   DEALLOCATE(GWFIBSDAT(IGRID)%IIBSCB)
   DEALLOCATE(GWFIBSDAT(IGRID)%IIBSOC)
   DEALLOCATE(GWFIBSDAT(IGRID)%ISUBFM)
   DEALLOCATE(GWFIBSDAT(IGRID)%ICOMFM)
   DEALLOCATE(GWFIBSDAT(IGRID)%IHCFM)
   DEALLOCATE(GWFIBSDAT(IGRID)%ISUBUN)
   DEALLOCATE(GWFIBSDAT(IGRID)%ICOMUN)
   DEALLOCATE(GWFIBSDAT(IGRID)%IHCUN)
   DEALLOCATE(GWFIBSDAT(IGRID)%IBQ)
   DEALLOCATE(GWFIBSDAT(IGRID)%IBQ1)
   DEALLOCATE(GWFIBSDAT(IGRID)%HC)
   DEALLOCATE(GWFIBSDAT(IGRID)%SCE)
   DEALLOCATE(GWFIBSDAT(IGRID)%SCV)
   DEALLOCATE(GWFIBSDAT(IGRID)%SUB)
   !
   RETURN
END
SUBROUTINE SGWF2IBS7PNT(IGRID)
   !  Set IBS pointers for grid.
   USE GWFIBSMODULE
   !
   IIBSCB=>GWFIBSDAT(IGRID)%IIBSCB
   IIBSOC=>GWFIBSDAT(IGRID)%IIBSOC
   ISUBFM=>GWFIBSDAT(IGRID)%ISUBFM
   ICOMFM=>GWFIBSDAT(IGRID)%ICOMFM
   IHCFM=>GWFIBSDAT(IGRID)%IHCFM
   ISUBUN=>GWFIBSDAT(IGRID)%ISUBUN
   ICOMUN=>GWFIBSDAT(IGRID)%ICOMUN
   IHCUN=>GWFIBSDAT(IGRID)%IHCUN
   IBQ=>GWFIBSDAT(IGRID)%IBQ
   IBQ1=>GWFIBSDAT(IGRID)%IBQ1
   HC=>GWFIBSDAT(IGRID)%HC
   SCE=>GWFIBSDAT(IGRID)%SCE
   SCV=>GWFIBSDAT(IGRID)%SCV
   SUB=>GWFIBSDAT(IGRID)%SUB
   !
   RETURN
END
SUBROUTINE SGWF2IBS7PSV(IGRID)
   !  Save IBS pointers for grid.
   USE GWFIBSMODULE
   !
   GWFIBSDAT(IGRID)%IIBSCB=>IIBSCB
   GWFIBSDAT(IGRID)%IIBSOC=>IIBSOC
   GWFIBSDAT(IGRID)%ISUBFM=>ISUBFM
   GWFIBSDAT(IGRID)%ICOMFM=>ICOMFM
   GWFIBSDAT(IGRID)%IHCFM=>IHCFM
   GWFIBSDAT(IGRID)%ISUBUN=>ISUBUN
   GWFIBSDAT(IGRID)%ICOMUN=>ICOMUN
   GWFIBSDAT(IGRID)%IHCUN=>IHCUN
   GWFIBSDAT(IGRID)%IBQ=>IBQ
   GWFIBSDAT(IGRID)%IBQ1=>IBQ1
   GWFIBSDAT(IGRID)%HC=>HC
   GWFIBSDAT(IGRID)%SCE=>SCE
   GWFIBSDAT(IGRID)%SCV=>SCV
   GWFIBSDAT(IGRID)%SUB=>SUB
   !
   RETURN
END
